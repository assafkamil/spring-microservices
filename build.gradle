buildscript {
    ext {
        springBootVersion = '1.3.5.RELEASE'
        springCloudVersion = 'Brixton.RELEASE'
        springGradleDepManagement = '0.5.7.RELEASE'
    }
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:${springGradleDepManagement}")
        classpath 'mysql:mysql-connector-java:5.1.34'
    }
}

plugins {
    id "org.flywaydb.flyway" version "4.0.3"
}

ext {
    mainClassName = "your.class.Name"
}

group 'com.geekoosh.edu.cloud.vacations'
version '0.0.1'

allprojects {
    repositories {
        jcenter()
        mavenLocal()
        maven { url "http://repo.spring.io/libs-snapshot" }
        maven { url "http://repo.spring.io/milestone" }
    }
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'spring-boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-starter-parent:${springCloudVersion}"
        }
    }

    dependencies {
        compile("org.springframework.boot:spring-boot-starter-web")
        compile("org.springframework.cloud:spring-cloud-starter-feign")
        compile("org.springframework.cloud:spring-cloud-commons")
        compile("org.springframework.cloud:spring-cloud-context")
        compile("org.springframework.boot:spring-boot-starter-data-jpa")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile group: 'junit', name: 'junit', version: '4.11'
    }

    bootRun {
        systemProperties = System.properties
    }

    afterEvaluate { project ->
        if (project.hasProperty('db') && project.db) {
            dependencies {
                compile("mysql:mysql-connector-java")
                compile("org.modelmapper:modelmapper:0.7.6")
                testCompile("com.h2database:h2")
                testCompile("org.flywaydb.flyway-test-extensions:flyway-dbunit-test:4.0")
            }
        }
    }
}
configure(subprojects.findAll {it.name != 'base-test' && it.name != 'base'}) {
    dependencies {
        compile project(":base")
        testCompile project(":base-test")
    }
}